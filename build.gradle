plugins {
	id "org.springframework.boot" version "4.0.2" apply false
	id "io.spring.dependency-management" version "1.1.7" apply false
}

allprojects {
	group = "com.vexor"
	version = "0.0.1-SNAPSHOT"

	repositories {
		mavenCentral()
	}
}

/**
 * Defaults comunes a TODO subproyecto
 */
subprojects {
	apply plugin: "io.spring.dependency-management"

	plugins.withId("java") {
		java {
			toolchain {
				languageVersion = JavaLanguageVersion.of(21)
			}
		}
	}
	plugins.withId("java-library") {
		java {
			toolchain {
				languageVersion = JavaLanguageVersion.of(21)
			}
		}
	}

	tasks.withType(Test).configureEach {
		useJUnitPlatform()
	}
}

/**
 * APPS (customer-API, employee-API): Spring Boot ejecutables
 */
configure(subprojects.findAll { it.path.startsWith(":apps:") }) {
	apply plugin: "org.springframework.boot"
	apply plugin: "java"

	tasks.named("bootJar").configure { enabled = true }
	tasks.named("jar").configure { enabled = false }

	dependencies {
		testImplementation "org.springframework.boot:spring-boot-starter-test"
	}
}

/**
 * LIBRARIES (modules + shared): NO ejecutables
 */
configure(subprojects.findAll { it.path.startsWith(":modules:") || it.path.startsWith(":shared:") }) {
	apply plugin: "java-library"

	tasks.matching { it.name == "bootJar" }.configureEach { enabled = false }
	tasks.matching { it.name == "jar" }.configureEach { enabled = true }
}
