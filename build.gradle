plugins {
	id "org.springframework.boot" version "3.4.2" apply false
	id "io.spring.dependency-management" version "1.1.7" apply false
}

ext {
	lombokVersion = "1.18.30"
}

allprojects {
	group = "com.vexor"
	version = "0.0.1-SNAPSHOT"

	repositories {
		mavenCentral()
	}
}

subprojects {
	apply plugin: "io.spring.dependency-management"

	dependencyManagement {
		imports {
			mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
		}
	}

	plugins.withId("java") {
		java {
			toolchain { languageVersion = JavaLanguageVersion.of(21) }
		}

		dependencies {
			compileOnly "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
			annotationProcessor "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
			testCompileOnly "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
			testAnnotationProcessor "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
		}
	}

	plugins.withId("java-library") {
		java {
			toolchain { languageVersion = JavaLanguageVersion.of(21) }
		}

		dependencies {
			compileOnly "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
			annotationProcessor "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
			testCompileOnly "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
			testAnnotationProcessor "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
		}
	}

	tasks.withType(Test).configureEach {
		useJUnitPlatform()
	}
}

configure(subprojects.findAll { it.path.startsWith(":apps:") }) {
	apply plugin: "org.springframework.boot"
	apply plugin: "java"

	tasks.named("bootJar").configure { enabled = true }
	tasks.named("jar").configure { enabled = false }

	dependencies {
		testImplementation "org.springframework.boot:spring-boot-starter-test"
	}
}

configure(subprojects.findAll { it.path.startsWith(":modules:") || it.path.startsWith(":shared:") }) {
	apply plugin: "java-library"

	tasks.matching { it.name == "bootJar" }.configureEach { enabled = false }
	tasks.matching { it.name == "jar" }.configureEach { enabled = true }
}